buildscript {
    ext["byte-buddy.version"] = "1.8.17" // Spring boot uses version 1.7.11 but version 1.8.0+ is required for java 10 support

    repositories {
        maven { url "http://${nexusHost}:${nexusPort}/repository/maven-public" }
        mavenLocal()
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.20.0" // Let it apply by default so that the plugin consolidates at the root of the project, otherwise it often hangs when checking dependency versions
    id "com.palantir.docker" version "0.20.1" apply(false)
    id "org.sonarqube" version "2.6.2" apply(false)
    id "org.springframework.boot" version "2.0.6.RELEASE" apply(false)
}

project("thule-gateway-config") {
    description = "Thule Gateway Config"

    // Plugins
    apply plugin: "java"
    apply plugin: "maven-publish"

    // Standard Thule gradle plugin config
    apply from: "${rootDir}/../gradle/build-config.gradle"

    // Non-standard Thule gradle plugin config
}

project("thule-gateway-service") {
    description = "Thule Gateway Service"

    // Plugins
    apply plugin: "com.palantir.docker"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "java"
    apply plugin: "jacoco"
    apply plugin: "org.sonarqube"
    apply plugin: "org.springframework.boot"
    apply plugin: "maven-publish"

    // Standard Thule gradle plugin config
    apply from: "${rootDir}/../gradle/build-config.gradle"

    // Non-standard Thule gradle plugin config

    // Dependencies
    dependencyManagement {
        imports {
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:Finchley.SR2")
        }
    }

    dependencies {
        compile("uk.co.serin.thule:thule-spring-boot-starter-oauth2:1.0.0-SNAPSHOT")
        compile("uk.co.serin.thule:thule-spring-boot-starter-utils:1.0.0-SNAPSHOT")
        compile("org.jolokia:jolokia-core") // Required for JMX support when using the Admin server
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.cloud:spring-cloud-starter-config")
        compile("org.springframework.cloud:spring-cloud-starter-netflix-eureka-client")
        compile('org.springframework.cloud:spring-cloud-starter-netflix-zuul')
        compile("org.springframework.cloud:spring-cloud-starter-sleuth")
        compile("org.springframework.cloud:spring-cloud-starter-zipkin")
        compile("org.springframework.retry:spring-retry")

        testCompile("uk.co.serin.thule:thule-spring-boot-starter-test:1.0.0-SNAPSHOT")

        itestCompile project(":thule-gateway-config")

        ctestCompile project(":thule-gateway-config")

        dtestCompile("uk.co.serin.thule:thule-spring-boot-starter-test:1.0.0-SNAPSHOT")
        dtestCompile("uk.co.serin.thule:thule-spring-boot-starter-utils:1.0.0-SNAPSHOT")
    }
}