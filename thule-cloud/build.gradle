buildscript {
    ext["byte-buddy.version"] = "1.8.17" // Spring boot uses version 1.7.11 but version 1.8.0+ is required for java 10 support

    repositories {
        maven { url "http://${nexusHost}:${nexusPort}/repository/maven-public" }
        mavenLocal()
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.20.0" // Let it apply by default so that the plugin consolidates at the root of the project, otherwise it often hangs when checking dependency versions
    id "org.sonarqube" version "2.6.2" apply(false)
    id "org.springframework.boot" version "2.0.4.RELEASE" apply(false)
}

project("thule-cloud-config") {
    description = "thule Cloud Config"

    // Plugins
    apply plugin: "java"
    apply plugin: "maven-publish"

    // Standard Thule gradle plugin config
    apply from: "${rootDir}/../gradle/build-config.gradle"

    // Non-standard thule gradle plugin config
    // Add Spring boot config from thule-configuration-config
    processResources {
        doLast {
            copy {
                from zipTree(configurations.compile.singleFile)
                into "$buildDir/resources/main"
            }
        }
    }

    // Dependencies
    dependencies {
        compile("uk.co.serin.thule:thule-configuration-config:1.0.0-SNAPSHOT") {
            transitive = false
        }
    }
}

project("thule-cloud-service") {
    description = "Thule Cloud Service"

    // Plugins
        apply plugin: "io.spring.dependency-management"
    apply plugin: "java"
    apply plugin: "org.sonarqube"
    apply plugin: "org.springframework.boot"
    apply plugin: "maven-publish"

    // Standard Thule gradle plugin config
    apply from: "${rootDir}/../gradle/build-config.gradle"

    // Non-standard GoHenry gradle plugin config
    bootJar.enabled = false // Don't need an "über-jar" but still want the rest of the spring boot plugin
    jar.enabled = true // Don't need an "über-jar" but still want the rest of the spring boot plugin

    // Dependencies
    dependencyManagement {
        imports {
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:Finchley.SR1")
        }
    }

    dependencies {
        dtestCompile project(":thule-cloud-config")
        dtestCompile("uk.co.serin.thule:thule-spring-boot-starter-test:1.0.0-SNAPSHOT")
        dtestCompile("uk.co.serin.thule:thule-spring-boot-starter-utils:1.0.0-SNAPSHOT")
        dtestCompile("org.springframework.boot:spring-boot-starter-web")
    }
}