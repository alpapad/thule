buildscript {
    // Override Spring boot versions
    ext["assertj.version"] = "3.8.0"// Remove when spring boot has caught up with the latest version
    ext["flyway.version"] = "4.2.0"// Remove when spring boot has caught up with the latest version
    ext["h2.version"] = "1.4.196"// Remove when spring boot has caught up with the latest h2 version
    ext["hibernate.version"] = "5.2.10.Final"  // Remove when spring boot has caught up with the latest version
    ext["httpclient.version"] = "4.5.3"  // Remove when spring boot has caught up with the latest version
    ext["hsqldb.version"] = "2.4.0"// Remove when spring boot has caught up with the latest version
    ext["jackson.version"] = "2.8.9"// Remove when spring boot has caught up with the latest version
    ext["mockito.version"] = "2.11.0"  // Remove when spring boot has caught up with the latest version
    ext["mysql.version"] = "8.0.7-dmr"  // Remove when spring boot has caught up with the latest version
    ext["spring-retry.version"] = "1.2.1.RELEASE"  // Remove when spring boot has caught up with the latest version

    ext {
        jacocoVersion = "0.7.9"
        springBootVersion = "1.5.7.RELEASE"
        springCloudVersion = "Dalston.SR3"
    }

    // Changing modules are cached by default for 24 hours, change the default to never and mark snapshots as changing (must be within the buildscript)
    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    repositories {
        maven {
            url "http://pooh:8081/repository/maven-public"
            credentials {
                username = nexusUser
                password = nexusPassword
            }
        }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("se.transmode.gradle:gradle-docker:1.2")
        classpath("uk.co.serin.gradle.plugins:sonarqube-build-breaker-plugin:1.0.0-SNAPSHOT") {
            changing = true
        }
    }
}

//############################## Plugins Configuration ##############################
// Core gradle plugins
plugins {
    id "com.github.ben-manes.versions" version "0.17.0"
    id "org.sonarqube" version "2.5"
}

// Sonarqube plugin configuration
sonarqube {
    properties {
        // SonarQube will fail due to duplicate code across micro-services, however, this is legal. So we will exclude cpd for those classes
        property "sonar.cpd.exclusions", "**/domain/**"
        property "sonar.host.url", "http://pooh:9000"
        property "sonar.scm.disabled", "true"
        property "sonar.projectName", "Thule"
    }
}

// Sonarqube build breaker plugin
apply plugin: "uk.co.serin.gradle.plugins.sonarqube-build-breaker"
// Sonarqube build breaker plugin configuration
tasks.sonarqubeBuildBreaker.dependsOn(tasks.sonarqube)

allprojects {
    //############################## Plugins Configuration ##############################
    // Java plugin (Note: Several plugins depend on the Java plugin so ensure it remains the first plugin defined)
    apply plugin: "java"

    // Java plugin configuration
    sourceCompatibility = 8.0
    targetCompatibility = 8.0
    // Replace the version in spring config with the gradle build version
    processResources.filter { String line -> line.replace("@version@", version.toString()) }

    // Dependency Management plugin
    apply plugin: "io.spring.dependency-management"
    // Dependencies Management plugin configuration
    dependencyManagement {
        imports {
            mavenBom("org.springframework.cloud:spring-cloud-starter-parent:${springCloudVersion}")
            // Use latest spring boot version to override spring boot version specified in spring-cloud-starter-parent
            mavenBom("org.springframework.boot:spring-boot-starter-parent:${springBootVersion}")
        }
        dependencies {
            dependency("com.bitmechanic:dumbster:1.9.0.2")
            dependency("com.github.tomakehurst:wiremock:2.10.1")
            dependency("com.googlecode.log4jdbc:log4jdbc:1.2")
            dependency("com.oracle:ojdbc6:11.2.0.1.0")
            dependency("de.codecentric:spring-boot-admin-server:1.5.4")
            dependency("de.codecentric:spring-boot-admin-server-ui:1.5.4")
            dependency("net.logstash.logback:logstash-logback-encoder:4.11")
            dependency("nl.jqno.equalsverifier:equalsverifier:2.3.3")
            dependency("org.awaitility:awaitility:3.0.0")
            dependency("org.codehaus.janino:janino:3.0.7")
            dependency("org.jolokia:jolokia-core:1.3.7")
            dependency("pl.pojo:pojo-tester:0.7.5")
        }
    }

    // Docker plugin
    apply plugin: "docker"
    // Docker plugin configuration
    task dockerBuild(type: Docker, dependsOn: jar) {
        // dockerBuild options
        applicationName = jar.baseName
        dockerfile = file("src/main/docker/Dockerfile")
        push = false
        registry = "pooh.serin-consultancy.co.uk"
        tagVersion = "latest"

        // Add files to Docker context
        doFirst {
            copy { from jar into stageDir }
            copy { from "src/main/docker/docker-entrypoint.sh" into stageDir }
        }

        // Only enable if a Dockerfile exists
        enabled = file(projectDir.toString() + '/src/main/docker/Dockerfile').isFile()

        // Add to build lifecycle
        assemble.dependsOn dockerBuild
    }

    // Jacoco plugin
    apply plugin: "jacoco"
    // Jacoco plugin configuration
    jacoco.toolVersion = jacocoVersion
    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    counter = "BRANCH"
                    minimum = 1.0 // 100% coverage
                }
                limit {
                    counter = "CLASS"
                    minimum = 1.0 // 100% coverage
                }
                limit {
                    counter = "COMPLEXITY"
                    minimum = 1.0 // 100% coverage
                }
                limit {
                    counter = "INSTRUCTION"
                    minimum = 1.0 // 100% coverage
                }
                limit {
                    counter = "LINE"
                    minimum = 1.0 // 100% coverage
                }
                limit {
                    counter = "METHOD"
                    minimum = 1.0 // 100% coverage
                }
            }
        }
    }
    // Always create a report even if jacocoTestCoverageVerification fails
    jacocoTestCoverageVerification.dependsOn jacocoTestReport
    check.dependsOn jacocoTestCoverageVerification

    // Maven Publish plugin
    apply plugin: 'maven-publish'
    // Maven plugin configuration
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                url("http://pooh:8081/repository/maven-${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}")
                credentials {
                    username = nexusUser
                    password = nexusPassword
                }
            }
        }
    }

    //############################## Build Configuration ##############################
    repositories {
        maven {
            url "http://pooh:8081/repository/maven-public"
            credentials {
                username = nexusUser
                password = nexusPassword
            }
        }
    }

    // Integration tests configuration
    configurations {
        itestCompile.extendsFrom testCompile
        itestRuntime.extendsFrom testRuntime
    }
    sourceSets {
        itest {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output

            java.srcDir file("src/itest/java")
            resources.srcDir file("src/itest/resources")
        }
    }
    task itest(type: Test, dependsOn: testClasses) {
        testClassesDirs = sourceSets.itest.output.classesDirs
        classpath = sourceSets.itest.runtimeClasspath
    }

    // Test configuration
    // Log test results summary
    tasks.withType(Test) {
        testLogging {
            // set options for log level LIFECYCLE
            events "passed", "skipped", "failed", "standardOut"
            exceptionFormat "short"
            showCauses true
            showExceptions true
            showStackTraces true

            // set options for log level DEBUG and INFO
            debug {
                events "started", "passed", "skipped", "failed", "standardOut", "standardError"
                exceptionFormat "full"
            }
            info.events = debug.events
            info.exceptionFormat = debug.exceptionFormat

            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                    def startItem = "|  ", endItem = "  |"
                    def repeatLength = startItem.length() + output.length() + endItem.length()
                    println("\n" + ("-" * repeatLength) + "\n" + startItem + output + endItem + "\n" + ("-" * repeatLength))
                }
            }
        }
    }

    // Project metadata
    description = "Thule"
    group = "uk.co.serin.thule"
    version = projectVersion + "-" + buildNumber
}

subprojects {
}

//############################## Project Configuration ##############################
project("thule-cloud") {
    tasks.itest.dependsOn(
            ":thule-admin-server:assemble", ":thule-admin-server:itest",
            ":thule-config-service:assemble", ":thule-config-service:itest",
            ":thule-discovery-service:assemble", ":thule-discovery-service:itest",
            ":thule-edge-server:assemble", ":thule-edge-server:itest",
            ":thule-email-service:assemble", ":thule-email-service:itest",
            ":thule-people-service:assemble", ":thule-people-service:itest")

    dependencies {
        compile("org.apache.httpcomponents:httpclient")
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.boot:spring-boot-starter-logging")
        compile("org.springframework.boot:spring-boot-starter-web") {
            exclude group: "org.springframework.boot", module: "spring-boot-starter-tomcat"
        }
        compile("org.springframework.boot:spring-boot-starter-jetty")

        testCompile project(":thule-test")
        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.awaitility:awaitility")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("org.springframework.retry:spring-retry")
        testCompile("pl.pojo:pojo-tester")
    }
}

project("thule-admin-server") {
    // Spring Boot plugin
    // org.springframework.boot must be after dependencyManagement see https://spring.io/blog/2015/11/25/migrating-spring-cloud-apps-from-spring-boot-1-2-to-1-3
    apply plugin: "org.springframework.boot"
    // Spring boot create META-INF/build-info.properties to automatically add to the spring actutator info
    springBoot {
        buildInfo()
    }

    dependencies {
        compile project(":thule-spring-boot-starter-utils")
        compile("de.codecentric:spring-boot-admin-server")
        compile("de.codecentric:spring-boot-admin-server-ui")
        compile("net.logstash.logback:logstash-logback-encoder")
        compile("org.jolokia:jolokia-core") // Required for JMX support when using the Admin server
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.boot:spring-boot-starter-logging")
        // Without aop, spring-retry will not work, see https://github.com/spring-projects/spring-retry/issues/52
        compile("org.springframework.boot:spring-boot-starter-aop")
        compile("org.springframework.cloud:spring-cloud-starter-config")
        compile("org.springframework.cloud:spring-cloud-starter-eureka")
        compile("org.springframework.cloud:spring-cloud-starter-sleuth")
        compile("org.springframework.cloud:spring-cloud-starter-zipkin")
        // Required to retry connecting to the discovery server when it is not available at startup
        compile("org.springframework.retry:spring-retry")

        // Add config to classpath for integration tests
        testCompile project(":thule-config-service").sourceSets.main.output
        testCompile project(":thule-test")
        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.awaitility:awaitility")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("pl.pojo:pojo-tester")
    }
}

project("thule-config-service") {
    // Spring Boot plugin
    // org.springframework.boot must be after dependencyManagement see https://spring.io/blog/2015/11/25/migrating-spring-cloud-apps-from-spring-boot-1-2-to-1-3
    apply plugin: "org.springframework.boot"
    // Spring boot create META-INF/build-info.properties to automatically add to the spring actutator info
    springBoot {
        buildInfo()
    }

    dependencies {
        compile project(":thule-spring-boot-starter-utils")
        compile("net.logstash.logback:logstash-logback-encoder")
        compile("org.jolokia:jolokia-core") // Required for JMX support when using the Admin server
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.boot:spring-boot-starter-logging")
        compile("org.springframework.cloud:spring-cloud-config-server")
        compile("org.springframework.cloud:spring-cloud-starter")
        compile("org.springframework.cloud:spring-cloud-starter-eureka")
        compile("org.springframework.cloud:spring-cloud-starter-sleuth")
        compile("org.springframework.cloud:spring-cloud-starter-zipkin")

        testCompile project(":thule-test")
        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.awaitility:awaitility")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("pl.pojo:pojo-tester")
    }
}

project("thule-discovery-service") {
    // Spring Boot plugin
    // org.springframework.boot must be after dependencyManagement see https://spring.io/blog/2015/11/25/migrating-spring-cloud-apps-from-spring-boot-1-2-to-1-3
    apply plugin: "org.springframework.boot"
    // Spring boot create META-INF/build-info.properties to automatically add to the spring actutator info
    springBoot {
        buildInfo()
    }

    // Required for Java9
//    tasks.withType(Test) {
//        jvmArgs += ["--add-modules", "java.xml.bind"]
//    }

    dependencies {
        compile project(":thule-spring-boot-starter-utils")
        compile("net.logstash.logback:logstash-logback-encoder")
        compile("org.jolokia:jolokia-core") // Required for JMX support when using the Admin server
        compile("org.springframework.boot:spring-boot-starter-actuator")
        // Without aop, spring-retry will not work, see https://github.com/spring-projects/spring-retry/issues/52
        compile("org.springframework.boot:spring-boot-starter-aop")
        compile("org.springframework.boot:spring-boot-starter-logging")
        compile("org.springframework.cloud:spring-cloud-starter-config")
        compile("org.springframework.cloud:spring-cloud-starter-eureka")
        compile("org.springframework.cloud:spring-cloud-starter-eureka-server")
        compile("org.springframework.cloud:spring-cloud-starter-sleuth")
        compile("org.springframework.cloud:spring-cloud-starter-zipkin")
        // Required to retry connecting to the discovery server when it is not available at startup
        compile("org.springframework.retry:spring-retry")

        // Add config to classpath for integration tests
        testCompile project(":thule-config-service").sourceSets.main.output
        testCompile project(":thule-test")
        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.awaitility:awaitility")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("pl.pojo:pojo-tester")
    }
}

project("thule-edge-server") {
    // Spring Boot plugin
    // org.springframework.boot must be after dependencyManagement see https://spring.io/blog/2015/11/25/migrating-spring-cloud-apps-from-spring-boot-1-2-to-1-3
    apply plugin: "org.springframework.boot"
    // Spring boot create META-INF/build-info.properties to automatically add to the spring actutator info
    springBoot {
        buildInfo()
    }

    dependencies {
        compile project(":thule-spring-boot-starter-utils")
        compile("net.logstash.logback:logstash-logback-encoder")
        compile("org.jolokia:jolokia-core") // Required for JMX support when using the Admin server
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.boot:spring-boot-starter-logging")
        compile("org.springframework.cloud:spring-cloud-starter-config")
        compile("org.springframework.cloud:spring-cloud-starter-eureka")
        compile("org.springframework.cloud:spring-cloud-starter-sleuth")
        compile('org.springframework.cloud:spring-cloud-starter-zuul')
        compile("org.springframework.cloud:spring-cloud-starter-zipkin")
        // Required to retry connecting to the discovery server when it is not available at startup
        compile("org.springframework.retry:spring-retry")

        // Add config to classpath for integration tests
        testCompile project(":thule-config-service").sourceSets.main.output
        testCompile project(":thule-test")
        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.awaitility:awaitility")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("pl.pojo:pojo-tester")
    }
}

project("thule-email-service") {
    // Spring Boot plugin
    // org.springframework.boot must be after dependencyManagement see https://spring.io/blog/2015/11/25/migrating-spring-cloud-apps-from-spring-boot-1-2-to-1-3
    apply plugin: "org.springframework.boot"
    // Spring boot create META-INF/build-info.properties to automatically add to the spring actutator info
    springBoot {
        buildInfo()
    }

    dependencies {
        compile project(":thule-spring-boot-starter-utils")
        compile("net.logstash.logback:logstash-logback-encoder")
        compile("org.jolokia:jolokia-core") // Required for JMX support when using the Admin server
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.boot:spring-boot-starter-logging")
        compile("org.springframework.boot:spring-boot-starter-mail")
        compile("org.springframework.cloud:spring-cloud-starter-config")
        compile("org.springframework.cloud:spring-cloud-starter-eureka")
        compile("org.springframework.cloud:spring-cloud-starter-sleuth")
        compile("org.springframework.cloud:spring-cloud-starter-zipkin")
        // Required to retry connecting to the discovery server when it is not available at startup
        compile("org.springframework.retry:spring-retry")

        // Add config to classpath for integration tests
        testCompile project(":thule-config-service").sourceSets.main.output
        testCompile project(":thule-test")
        testCompile("com.bitmechanic:dumbster")
        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.awaitility:awaitility")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("pl.pojo:pojo-tester")
    }
}

project("thule-people-service") {
    // Spring Boot plugin
    // org.springframework.boot must be after dependencyManagement see https://spring.io/blog/2015/11/25/migrating-spring-cloud-apps-from-spring-boot-1-2-to-1-3
    apply plugin: "org.springframework.boot"
    // Spring boot create META-INF/build-info.properties to automatically add to the spring actutator info
    springBoot {
        buildInfo()
    }

    // Required for Java9
//    tasks.withType(Test) {
//        jvmArgs += ["--add-modules", "java.xml.bind"]
//    }

    dependencies {
        compile project(":thule-spring-boot-starter-utils")
        // Spring boot will add java8 java.util.time Jackson support when this dependency has been added
        compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")
        compile("com.googlecode.log4jdbc:log4jdbc")
        compile("com.h2database:h2")
        compile("net.logstash.logback:logstash-logback-encoder")
        compile("org.apache.httpcomponents:httpclient")
        compile("org.flywaydb:flyway-core")
        compile("org.jolokia:jolokia-core") // Required for JMX support when using the Admin server
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.boot:spring-boot-starter-aop")
        compile("org.springframework.boot:spring-boot-starter-data-jpa")
        compile("org.springframework.boot:spring-boot-starter-data-rest")
        compile("org.springframework.boot:spring-boot-starter-logging")
        compile("org.springframework.boot:spring-boot-starter-security")
        compile("org.springframework.boot:spring-boot-starter-validation")
        compile("org.springframework.cloud:spring-cloud-starter-config")
        compile("org.springframework.cloud:spring-cloud-starter-eureka")
        compile("org.springframework.cloud:spring-cloud-starter-feign")
        compile("org.springframework.cloud:spring-cloud-starter-sleuth")
        compile("org.springframework.cloud:spring-cloud-starter-zipkin")
        compile("org.springframework.data:spring-data-rest-hal-browser")
        // Required to retry connecting to the discovery server when it is not available at startup
        compile("org.springframework.retry:spring-retry")

        // Add config to classpath for integration tests
        testCompile project(":thule-config-service").sourceSets.main.output
        testCompile project(":thule-test")
        testCompile("com.github.tomakehurst:wiremock")
        testCompile("com.googlecode.log4jdbc:log4jdbc")
        testCompile("com.oracle:ojdbc6")
        testCompile("mysql:mysql-connector-java")
        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.awaitility:awaitility")
        testCompile("org.hsqldb:hsqldb")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("pl.pojo:pojo-tester")
    }
}

project("thule-repository-mongodb") {
    dependencies {
        compile project(":thule-spring-boot-starter-utils")
        compile("net.logstash.logback:logstash-logback-encoder")
        compile("org.springframework.boot:spring-boot-starter-data-mongodb")
        compile("org.springframework.boot:spring-boot-starter-logging")
        compile("org.springframework.boot:spring-boot-starter-validation")

        testCompile project(":thule-test")
        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.awaitility:awaitility")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("pl.pojo:pojo-tester")
    }
}

project("thule-spring-boot-autoconfigure") {
    dependencies {
        compile project(":thule-utils")

        testCompile project(":thule-test")
        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.awaitility:awaitility")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("pl.pojo:pojo-tester")
    }
}

project("thule-spring-boot-starter-utils") {
    dependencies {
        compile project(":thule-spring-boot-autoconfigure")
        compile project(":thule-utils")

        testCompile project(":thule-test")
        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.awaitility:awaitility")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("pl.pojo:pojo-tester")
    }
}

project("thule-test") {
    dependencies {
        compile("nl.jqno.equalsverifier:equalsverifier")
        compile("org.springframework.boot:spring-boot-starter-test")

        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.awaitility:awaitility")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("pl.pojo:pojo-tester")
    }
}

project("thule-utils") {
    dependencies {
        compile("org.codehaus.janino:janino") // Required by logback if-condition statements in the xml configuration
        compile("org.springframework.boot:spring-boot-starter-aop")
        compile("org.springframework.boot:spring-boot-starter-logging")
        compile("org.springframework.boot:spring-boot-starter-web") {
            exclude group: "org.springframework.boot", module: "spring-boot-starter-tomcat"
        }
        compile("org.springframework.boot:spring-boot-starter-jetty")

        testCompile project(":thule-test")
        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.awaitility:awaitility")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("pl.pojo:pojo-tester")
    }
}
