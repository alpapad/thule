buildscript {
    ext["byte-buddy.version"] = "1.8.17" // Spring boot uses version 1.7.11 but version 1.8.0+ is required for java 10 support

    repositories {
        maven { url "http://${nexusHost}:${nexusPort}/repository/maven-public" }
        mavenLocal()
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.20.0" apply(false)
    id "com.palantir.docker" version "0.20.1" apply(false)
    id "org.sonarqube" version "2.6.2" apply(false)
    id "org.springframework.boot" version "2.0.4.RELEASE" apply(false)
}

subprojects {
    apply plugin: "com.github.ben-manes.versions"
    apply plugin: "com.palantir.docker"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "jacoco"
    apply plugin: "java"
    apply plugin: "maven-publish"
    apply plugin: "org.sonarqube"
    apply plugin: "org.springframework.boot"

    // Standard Serin gradle plugin config
    apply from: "${rootDir}/../gradle/build-config.gradle"

    // Dependencies
    dependencyManagement {
        imports {
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:Finchley.SR1")
        }
    }

    // Non-standard Serin gradle plugin config
    bootJar.enabled = false // Don't need an "über-jar" but still want the rest of the spring boot plugin
    jar.enabled = true // Don't need an "über-jar" but still want the rest of the spring boot plugin
}

project("thule-spring-boot-starter-oauth2") {
    description = "Thule Spring Boot Starter Oauth2"
    dependencies {
        compile project(":thule-utils")
        compile("org.springframework.cloud:spring-cloud-starter-oauth2")

        testCompile project(":thule-spring-boot-starter-test")

        itestCompile project(":thule-oauth2-test")
    }
}

project("thule-spring-boot-starter-oauth2-test") {
    description = "Thule Spring Boot Starter Oauth2 Test"
    dependencies {
        compile project(":thule-oauth2-test")

        testCompile project(":thule-spring-boot-starter-test")
    }
}


project("thule-spring-boot-starter-test") {
    description = "Thule Spring Boot Starter Test"
    dependencies {
        compile project(":thule-test")
        compile("nl.jqno.equalsverifier:equalsverifier:2.5.1")
        compile("org.awaitility:awaitility:3.1.2")
        compile("org.springframework.boot:spring-boot-starter-test")
        compile("org.springframework.cloud:spring-cloud-contract-wiremock")
        compile("pl.pojo:pojo-tester:0.7.6")
    }
}

project("thule-spring-boot-starter-utils") {
    description = "Thule Spring Boot Starter Utils"
    dependencies {
        compile project(":thule-utils")

        testCompile project(":thule-spring-boot-starter-test")
    }
}

project("thule-oauth2-test") {
    description = "Thule OAuth2 Test"
    dependencies {
        compile("org.springframework.cloud:spring-cloud-starter-oauth2")

        testCompile project(":thule-spring-boot-starter-test")
    }
}

project("thule-test") {
    description = "Thule Test"
    dependencies {
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.boot:spring-boot-starter-test")
        compile("org.springframework.boot:spring-boot-starter-web") {
            // spring-webmvc is not required and actually causes a problem with thule-gateway-service
            // which is incompatible with spring-webmvc, hence it has been excluded
            exclude group: "org.springframework", module: "spring-webmvc"
        }
        compile("org.springframework.retry:spring-retry")

        testCompile("pl.pojo:pojo-tester:0.7.6")
    }
}

project("thule-utils") {
    description = "Thule Utils"
    dependencies {
        compile("com.sun.xml.bind:jaxb-core:2.3.0.1") // Required as part of SOAP (jaxb) when using java 9+
        compile("com.sun.xml.bind:jaxb-impl:2.3.0.1") // Required as part of SOAP (jaxb) when using java 9+
        compile("commons-discovery:commons-discovery:0.4")
        compile("javax.activation:javax.activation-api:1.2.0") // Required as part of SOAP (jaxp) when using java 9+
        compile("javax.xml.bind:jaxb-api") // Required as part of SOAP (jaxb) when using java 9+
        compile("javax.xml.soap:javax.xml.soap-api:1.4.0") // Required as part of SOAP (jaxws) when using java 9+
        compile("net.logstash.logback:logstash-logback-encoder:5.2")
        compile("org.codehaus.janino:janino:3.0.8") // Required by logback if-condition statements in the xml configuration
        compile("org.apache.axis:axis:1.4")
        compile("org.apache.axis:axis-jaxrpc:1.4")
        compile("org.hibernate:hibernate-core")
        compile("org.springframework.boot:spring-boot-starter-aop")
        compile("org.springframework.boot:spring-boot-starter-logging")
        compile("org.springframework.boot:spring-boot-starter-web")
        compile("xml-apis:xml-apis") // Required as part of SOAP (jax) when using java 9+

        testCompile project(":thule-spring-boot-starter-test")
    }
}