buildscript {
    ext["spring-security-oauth2-autoconfigure.version"] = "2.1.2.RELEASE" // The pom for org.springframework.cloud:spring-cloud-security-dependencies:2.1.0.RELEASE incorrectly specifies <spring-security-oauth2-autoconfigure.version>2.1.0.M4</spring-security-oauth2-autoconfigure.version>

    repositories {
        maven { url "http://${nexusHost}:${nexusPort}/repository/maven-public" }
        mavenLocal()
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.20.0" // Let it apply by default so that the plugin consolidates at the root of the project, otherwise it often hangs when checking dependency versions
    id "com.palantir.docker" version "0.21.0" apply(false)
    id "org.sonarqube" version "2.7" apply(false)
    id "org.springframework.boot" version "2.1.3.RELEASE" apply(false)
}

subprojects {
    apply plugin: "com.palantir.docker"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "jacoco"
    apply plugin: "java"
    apply plugin: "maven-publish"
    apply plugin: "org.sonarqube"
    apply plugin: "org.springframework.boot"

    // Standard Serin gradle plugin config
    apply from: "${rootDir}/../gradle/build-config.gradle"

    // Non-standard Serin gradle plugin config
    bootJar.enabled = false // Don't need an "über-jar" but still want the rest of the spring boot plugin
    jar.enabled = true // Don't need an "über-jar" but still want the rest of the spring boot plugin

    // Dependencies
    dependencyManagement {
        imports {
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:Greenwich.RELEASE")
        }
    }
}

project("thule-spring-boot-starter") {
    description = "Thule Spring Boot Starter Utils"
    dependencies {
        compile project(":thule-utils")

        compileOnly("org.springframework.data:spring-data-jpa")
        compileOnly("org.springframework.security:spring-security-core")

        testCompile project(":thule-spring-boot-starter-test")
    }
}

project("thule-spring-boot-starter-oauth2") {
    description = "Thule Spring Boot Starter Oauth2"
    dependencies {
        compile project(":thule-spring-boot-starter")
        compile("org.springframework.cloud:spring-cloud-starter-oauth2")

        testCompile project(":thule-spring-boot-starter-test")
    }
}

project("thule-spring-boot-starter-oauth2-test") {
    description = "Thule Spring Boot Starter Oauth2 Test"
    dependencies {
        compile project(":thule-spring-boot-starter")
        compile project(":thule-spring-boot-starter-test")
        compile("org.springframework.security:spring-security-test")
    }
}

project("thule-spring-boot-starter-test") {
    description = "Thule Spring Boot Starter Test"
    dependencies {
        compile project(":thule-spring-boot-starter")
        compile project(":thule-test")
        compile("nl.jqno.equalsverifier:equalsverifier:3.1.4") // Remove once we have moved to lombok
        compile("org.springframework.boot:spring-boot-starter-test")
        compile("org.springframework.cloud:spring-cloud-contract-wiremock")
        compile("org.springframework.restdocs:spring-restdocs-mockmvc")
        compile("pl.pojo:pojo-tester:0.7.6") // Remove once we have moved to lombok
    }
}

project("thule-test") {
    description = "Thule Test"
    dependencies {
        compile project(":thule-utils")
        compileOnly("org.springframework.cloud:spring-cloud-starter-oauth2")
        compile("org.awaitility:awaitility:3.1.6")
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.boot:spring-boot-starter-test")
        compile("org.springframework.boot:spring-boot-starter-web")
        compile("org.springframework.retry:spring-retry")

        compileOnly("org.projectlombok:lombok")

        testCompile("org.springframework.cloud:spring-cloud-starter-oauth2")
        testCompile("pl.pojo:pojo-tester:0.7.6")
    }
}

project("thule-utils") {
    description = "Thule Utils"
    dependencies {
        compileOnly("org.springframework.boot:spring-boot-starter-data-jpa")
        compileOnly("org.springframework.cloud:spring-cloud-starter-oauth2")
        compileOnly("org.springframework.cloud:spring-cloud-starter-openfeign")
 
        compile("com.sun.xml.bind:jaxb-core:2.3.0.1") // Required by anything using JAXB when using java 9+
        compile("com.sun.xml.bind:jaxb-impl:2.3.0.1") // Required by anything using JAXB when using java 9+
        compile("javax.xml.bind:jaxb-api") // Required by anything using JAXB when using java 9+
        compile("net.logstash.logback:logstash-logback-encoder:5.3")
        compile("org.codehaus.janino:janino") // Required by logback if-condition statements in the logback xml configuration
        compile("org.hibernate:hibernate-core")
        compile("org.springframework.boot:spring-boot-starter-aop")
        compile("org.springframework.boot:spring-boot-starter-logging")
        compile("org.springframework.boot:spring-boot-starter-validation")
        compile("org.springframework.boot:spring-boot-starter-web")

        compileOnly("org.projectlombok:lombok")

        testCompile("org.springframework.boot:spring-boot-starter-data-jpa")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("org.springframework.cloud:spring-cloud-starter-oauth2")
        testCompile("org.springframework.cloud:spring-cloud-starter-openfeign")

        itestCompile("com.h2database:h2")
        itestCompile("org.springframework.boot:spring-boot-starter-data-jpa")
    }
}