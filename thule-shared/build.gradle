buildscript {
    ext["mockito.version"] = "3.1.0" // Spring boot 2.1.6 uses mockito version 2.23.4 which does not fully support java 12

    repositories {
        maven { url "http://${nexusHost}:${nexusPort}/repository/maven-public" }
        mavenLocal()
    }

    dependencies {
        classpath("uk.co.serin.gradle.plugins:sonarqube-build-breaker-plugin:1.0.0-SNAPSHOT")
        classpath("uk.co.serin.gradle.scripts:serin-gradle-scripts:1.0.0-SNAPSHOT")
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.27.0"
    id "org.sonarqube" version "2.8" apply(false)
    id "org.springframework.boot" version "2.2.1.RELEASE" apply(false)
}

subprojects {
    apply plugin: "io.spring.dependency-management"
    apply plugin: "jacoco"
    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "org.sonarqube"
    apply plugin: "org.springframework.boot"
    apply plugin: "uk.co.serin.gradle.plugins.sonarqube-build-breaker"

    // Standard Serin gradle plugin config
    apply from: buildscript.classLoader.getResource("uk/co/serin/gradle/scripts/build-config.gradle")

    // Non-standard Serin gradle plugin config
    bootJar.enabled = false // Don't need an "über-jar" but still want the rest of the spring boot plugin
    jar.enabled = true // Don't need an "über-jar" but still want the rest of the spring boot plugin

    // Dependencies
    dependencyManagement {
        imports {
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:Greenwich.SR3")
        }
    }
}

project("thule-oauth2") {
    description = "Thule Oauth2"
    dependencies {
        annotationProcessor("org.projectlombok:lombok")

        compileOnly("org.projectlombok:lombok")

        implementation("org.springframework.cloud:spring-cloud-starter-oauth2")

        testImplementation("org.springframework.boot:spring-boot-starter-test")
    }
}

project("thule-spring-boot-starter") {
    description = "Thule Spring Boot Starter Utils"
    dependencies {
        annotationProcessor("org.projectlombok:lombok")

        api project(":thule-utils")
        api("org.springframework.boot:spring-boot-starter")

        compileOnly("org.projectlombok:lombok")
    }
}

project("thule-spring-boot-starter-oauth2") {
    description = "Thule Spring Boot Starter Oauth2"
    dependencies {
        annotationProcessor("org.projectlombok:lombok")

        api project(":thule-oauth2")
        api project(":thule-spring-boot-starter")
        api("org.springframework.boot:spring-boot-starter-web")
        api("org.springframework.cloud:spring-cloud-starter-oauth2")

        compileOnly("org.projectlombok:lombok")

        testImplementation project(":thule-spring-boot-starter-test")

        itestImplementation("org.apache.httpcomponents:httpclient")
    }
}

project("thule-spring-boot-starter-oauth2-test") {
    description = "Thule Spring Boot Starter Oauth2 Test"
    dependencies {
        api project(":thule-spring-boot-starter")
        api project(":thule-spring-boot-starter-test")
        api("org.springframework.security:spring-security-test")
    }
}

project("thule-spring-boot-starter-test") {
    description = "Thule Spring Boot Starter Test"
    dependencies {
        api project(":thule-spring-boot-starter")
        api project(":thule-test")
        api("org.awaitility:awaitility:4.0.1")
        api("org.springframework.boot:spring-boot-starter-test")
    }
}

project("thule-test") {
    description = "Thule Test"
    dependencies {
        annotationProcessor("org.projectlombok:lombok")

        api("org.springframework.boot:spring-boot-starter-actuator")
        api("org.springframework.boot:spring-boot-starter-web")

        compileOnly("org.projectlombok:lombok")

        implementation("org.springframework.boot:spring-boot-starter-test")
        implementation("org.springframework.retry:spring-retry")
    }
}

project("thule-utils") {
    description = "Thule Utils"
    dependencies {
        annotationProcessor("org.projectlombok:lombok")

        implementation("net.logstash.logback:logstash-logback-encoder:6.2")
        implementation("org.codehaus.janino:janino") // Required by logback if-condition statements in the logback xml configuration
        implementation("org.glassfish.jaxb:jaxb-runtime") // Required by anything using JAXB when using java 9+
        implementation("org.springframework.boot:spring-boot-starter-aop")
        implementation("org.springframework.boot:spring-boot-starter-logging")
        implementation("org.springframework.boot:spring-boot-starter-validation")

        compileOnly("org.projectlombok:lombok")

        testImplementation("org.springframework.boot:spring-boot-starter-test")
    }
}