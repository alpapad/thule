buildscript {
    // Override Spring boot versions
    ext["assertj.version"] = "3.5.2"  // Remove when spring boot has caught up with the latest version
    ext["flyway.version"] = "4.1.2"  // Remove when spring boot has caught up with the latest version
    ext["h2.version"] = "1.4.194"  // Remove when spring boot has caught up with the latest h2 version
    ext["hibernate.version"] = "5.2.10.Final"  // Remove when spring boot has caught up with the latest version
    ext["httpclient.version"] = "4.5.3"  // Remove when spring boot has caught up with the latest version
    ext["hsqldb.version"] = "2.4.0"  // Remove when spring boot has caught up with the latest version
    ext["jackson.version"] = "2.8.8" // Remove when spring boot has caught up with the latest version
    ext["mockito.version"] = "2.7.13"  // Remove when spring boot has caught up with the latest version
    ext["mysql.version"] = "6.0.6"  // Remove when spring boot has caught up with the latest version
    ext["spring-retry.version"] = "1.2.0.RELEASE"  // Remove when spring boot has caught up with the latest version

    ext {
        jacocoSkip = false
        jacocoVersion = "0.7.9"
        springBootVersion = "1.5.2.RELEASE"
        springCloudVersion = "Dalston.RELEASE"
    }

    repositories {
        mavenLocal()
        maven {
            credentials {
                username = nexusUser
                password = nexusPassword

            }
            url "http://pooh:8081/repository/maven-public"
        }
        mavenCentral()
//        maven {
//            url "http://repo.spring.io/milestone"
//        }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("se.transmode.gradle:gradle-docker:1.2")
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.14.0"
    id "org.sonarqube" version "2.2.1"
}

allprojects {
    apply plugin: "java"

    // Project metadata
    description = "Thule"
    group = "uk.co.serin.thule"
    version = projectVersion + "-" + buildNumber

    // Java plugin configuration
    sourceCompatibility =  1.8
    targetCompatibility = 1.8
    // Replace the version in spring config with the gradle build version
    processResources.filter { String line -> line.replace("@version@", version) }

    repositories {
        mavenLocal()
        maven {
            credentials {
                username = nexusUser
                password = nexusPassword

            }
            url "http://pooh:8081/repository/maven-public"
        }
        mavenCentral()
//        maven {
//            url "http://repo.spring.io/milestone"
//        }
    }

    // Sonarqube plugin configuration
    sonarqube {
        properties {
            property "sonar.host.url", "http://pooh:9000"
            property "sonar.scm.disabled", "true"
            property "sonar.projectName", "Thule"
        }
    }
}

subprojects {
    apply plugin: "docker"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "jacoco"
    apply plugin: "maven"

    // Dependencies
    dependencyManagement {
        imports {
            mavenBom("org.springframework.cloud:spring-cloud-starter-parent:${springCloudVersion}")
            // Use latest spring boot version to override spring boot version specified in spring-cloud-starter-parent
            mavenBom("org.springframework.boot:spring-boot-starter-parent:${springBootVersion}")
        }
        dependencies {
            dependency("com.googlecode.log4jdbc:log4jdbc:1.2")
            dependency("com.oracle:ojdbc6:11.2.0.1.0")
            dependency("commons-lang:commons-lang:2.6")
            dependency("de.codecentric:spring-boot-admin-server:1.5.0")
            dependency("de.codecentric:spring-boot-admin-server-ui:1.5.0")
            dependency("net.logstash.logback:logstash-logback-encoder:4.9")
            dependency("nl.jqno.equalsverifier:equalsverifier:2.2.2")
            dependency("org.jolokia:jolokia-core:1.3.6")
            dependency("org.springframework.data:spring-data-rest-hal-browser:2.6.3.RELEASE")
        }
    }

    // org.springframework.boot must be after dependencyManagement see https://spring.io/blog/2015/11/25/migrating-spring-cloud-apps-from-spring-boot-1-2-to-1-3
    apply plugin: "org.springframework.boot"

    // Docker plugin configuration
    task dockerBuild(type: Docker, dependsOn: assemble) {
        // dockerBuild options
        applicationName = jar.baseName
        dockerfile = file("src/main/docker/Dockerfile")
        push = false
        registry = "pooh.serin-consultancy.co.uk"
        tagVersion = "latest"

        // Add files to Docker context
        doFirst {
            copy { from jar into stageDir }
            copy { from "src/main/docker/docker-entrypoint.sh" into stageDir }
        }

        // Only enable if a Dockerfile exists
        enabled = file(projectDir.toString() + '/src/main/docker/Dockerfile').isFile()

        // Add to build lifecycle
        build.dependsOn dockerBuild
    }

    // Integration tests configuration
    configurations {
        itestCompile.extendsFrom testCompile
        itestRuntime.extendsFrom testRuntime
    }
    sourceSets {
        itest {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output

            java.srcDir file("src/itest/java")
            resources.srcDir file("src/itest/resources")
        }
    }
    task itest(type: Test, dependsOn: test) {
        testClassesDir = sourceSets.itest.output.classesDir
        classpath = sourceSets.itest.runtimeClasspath

        // Add to build lifecycle
        check.dependsOn itest
    }

    // Jacoco plugin configuration
    jacoco.toolVersion = jacocoVersion
    test.jacoco.enabled = !jacocoSkip
    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 1.0 // 100% coverage
                }
            }
        }
    }
    jacocoTestCoverageVerification.dependsOn jacocoTestReport // Always create a report even if jacocoTestCoverageVerification fails
    check.dependsOn jacocoTestCoverageVerification

    // Test configuration
    // Log test results summary
    tasks.withType(Test) {
        testLogging {
            // set options for log level LIFECYCLE
            events "passed", "skipped", "failed", "standardOut"
            showExceptions true
            exceptionFormat "short"
            showCauses true
            showStackTraces true

            // set options for log level DEBUG and INFO
            debug {
                events "started", "passed", "skipped", "failed", "standardOut", "standardError"
                exceptionFormat "full"
            }
            info.events = debug.events
            info.exceptionFormat = debug.exceptionFormat

            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                    def startItem = "|  ", endItem = "  |"
                    def repeatLength = startItem.length() + output.length() + endItem.length()
                    println("\n" + ("-" * repeatLength) + "\n" + startItem + output + endItem + "\n" + ("-" * repeatLength))
                }
            }
        }
    }

    // Spring boot create META-INF/build-info.properties to automatically add to the spring actutator info
    springBoot {
        buildInfo()
    }
}

project("thule-cloud") {
    tasks.itest.dependsOn(":thule-admin-server:build", ":thule-config-service:build", ":thule-discovery-service:build", ":thule-edge-server:build", ":thule-people-service:build")

    bootRepackage.enabled = false

    dependencies {
        compile("org.apache.httpcomponents:httpclient")
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.boot:spring-boot-starter-logging")
        compile("org.springframework.boot:spring-boot-starter-web")

        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("org.springframework.retry:spring-retry")
    }
}

project("thule-admin-server") {
    dependencies {
        compile project(":thule-core")
        compile("de.codecentric:spring-boot-admin-server")
        compile("de.codecentric:spring-boot-admin-server-ui")
        compile("net.logstash.logback:logstash-logback-encoder")
        compile("org.jolokia:jolokia-core") // Required for JMX support when using the Admin server
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.boot:spring-boot-starter-aop") // Without aop, spring-retry will not work, see https://github.com/spring-projects/spring-retry/issues/52
        compile("org.springframework.boot:spring-boot-starter-logging")
        compile("org.springframework.cloud:spring-cloud-starter-config")
        compile("org.springframework.cloud:spring-cloud-starter-eureka")
        compile("org.springframework.cloud:spring-cloud-starter-sleuth")
        compile("org.springframework.retry:spring-retry") // Required to retry connecting to the discovery server when it is not available at startup

        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.springframework.boot:spring-boot-starter-test")
    }
}

project("thule-config-service") {
    dependencies {
        compile project(":thule-core")
        compile("net.logstash.logback:logstash-logback-encoder")
        compile("org.jolokia:jolokia-core") // Required for JMX support when using the Admin server
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.boot:spring-boot-starter-logging")
        compile("org.springframework.cloud:spring-cloud-config-server")
        compile("org.springframework.cloud:spring-cloud-starter")
        compile("org.springframework.cloud:spring-cloud-starter-eureka")
        compile("org.springframework.cloud:spring-cloud-starter-sleuth")

        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.springframework.boot:spring-boot-starter-test")
    }
}

project("thule-core") {
    bootRepackage.enabled = false

    dependencies {
        compile("commons-lang:commons-lang")
        compile("org.springframework.boot:spring-boot-starter-aop")

        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.springframework.boot:spring-boot-starter-test")
    }
}

project("thule-discovery-service") {
    dependencies {
        compile project(":thule-core")
        compile("net.logstash.logback:logstash-logback-encoder")
        compile("org.jolokia:jolokia-core") // Required for JMX support when using the Admin server
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.boot:spring-boot-starter-aop") // Without aop, spring-retry will not work, see https://github.com/spring-projects/spring-retry/issues/52
        compile("org.springframework.boot:spring-boot-starter-logging")
        compile("org.springframework.cloud:spring-cloud-starter-config")
        compile("org.springframework.cloud:spring-cloud-starter-eureka")
        compile("org.springframework.cloud:spring-cloud-starter-eureka-server")
        compile("org.springframework.cloud:spring-cloud-starter-sleuth")
        compile("org.springframework.retry:spring-retry") // Required to retry connecting to the discovery server when it is not available at startup

        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.springframework.boot:spring-boot-starter-test")
    }
}

project("thule-edge-server") {
    dependencies {
        compile project(":thule-core")
        compile("net.logstash.logback:logstash-logback-encoder")
        compile("org.jolokia:jolokia-core") // Required for JMX support when using the Admin server
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.boot:spring-boot-starter-logging")
        compile("org.springframework.cloud:spring-cloud-starter-config")
        compile("org.springframework.cloud:spring-cloud-starter-eureka")
        compile("org.springframework.cloud:spring-cloud-starter-sleuth")
        compile('org.springframework.cloud:spring-cloud-starter-zuul')
        compile("org.springframework.retry:spring-retry") // Required to retry connecting to the discovery server when it is not available at startup

        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.springframework.boot:spring-boot-starter-test")
    }
}

project("thule-email-service") {
    dependencies {
        compile project(":thule-core")
        compile("net.logstash.logback:logstash-logback-encoder")
        compile("org.jolokia:jolokia-core") // Required for JMX support when using the Admin server
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.boot:spring-boot-starter-logging")
        compile("org.springframework.boot:spring-boot-starter-mail")
        compile("org.springframework.cloud:spring-cloud-starter-config")
        compile("org.springframework.cloud:spring-cloud-starter-eureka")
        compile("org.springframework.cloud:spring-cloud-starter-sleuth")
        compile("org.springframework.retry:spring-retry") // Required to retry connecting to the discovery server when it is not available at startup

        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.springframework.boot:spring-boot-starter-test")
    }
}

project("thule-people-service") {
    dependencies {
        compile project(":thule-core")
        compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310") // Spring boot will add java8 java.util.time Jackson support when this dependency has been added
        compile("com.googlecode.log4jdbc:log4jdbc")
        compile("com.h2database:h2")
        compile("net.logstash.logback:logstash-logback-encoder")
        compile("org.apache.httpcomponents:httpclient")
        compile("org.flywaydb:flyway-core")
        compile("org.hibernate:hibernate-java8")
        compile("org.jolokia:jolokia-core") // Required for JMX support when using the Admin server
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.boot:spring-boot-starter-aop")
        compile("org.springframework.boot:spring-boot-starter-data-jpa")
        compile("org.springframework.boot:spring-boot-starter-data-rest")
        compile("org.springframework.boot:spring-boot-starter-logging")
        compile("org.springframework.boot:spring-boot-starter-security")
        compile("org.springframework.boot:spring-boot-starter-validation")
        compile("org.springframework.cloud:spring-cloud-starter-config")
        compile("org.springframework.cloud:spring-cloud-starter-eureka")
        compile("org.springframework.cloud:spring-cloud-starter-sleuth")
        compile("org.springframework.data:spring-data-rest-hal-browser")
        compile("org.springframework.retry:spring-retry") // Required to retry connecting to the discovery server when it is not available at startup

        testCompile("com.googlecode.log4jdbc:log4jdbc")
        testCompile("com.oracle:ojdbc6")
        testCompile("mysql:mysql-connector-java")
        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.hsqldb:hsqldb")
        testCompile("org.springframework.boot:spring-boot-starter-test")
    }
}

project("thule-repository-mongodb") {
    bootRepackage.enabled = false

    dependencies {
        compile project(":thule-core")
        compile("net.logstash.logback:logstash-logback-encoder")
        compile("org.springframework.boot:spring-boot-starter-data-mongodb")
        compile("org.springframework.boot:spring-boot-starter-logging")
        compile("org.springframework.boot:spring-boot-starter-validation")

        testCompile("nl.jqno.equalsverifier:equalsverifier")
        testCompile("org.springframework.boot:spring-boot-starter-test")
    }
}
