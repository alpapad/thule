buildscript {
    repositories {
        maven { url "http://${nexusHost}:${nexusPort}/repository/maven-public" }
        mavenLocal()
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.20.0" // Let it apply by default so that the plugin consolidates at the root of the project, otherwise it often hangs when checking dependency versions
    id "com.palantir.docker" version "0.21.0" apply(false)
    id "org.sonarqube" version "2.7" apply(false)
    id "org.springframework.boot" version "2.1.2.RELEASE" apply(false)
}

project("thule-people-config") {
    description = "Thule People Config"

    // Plugins
    apply plugin: "java"
    apply plugin: "maven-publish"

    // Standard Thule gradle plugin config
    apply from: "${rootDir}/../gradle/build-config.gradle"

    // Non-standard Thule gradle plugin config
}

project("thule-people-service") {
    description = "Thule People Service"

    // Plugins
    apply plugin: "com.palantir.docker"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "java"
    apply plugin: "jacoco"
    apply plugin: "org.sonarqube"
    apply plugin: "org.springframework.boot"
    apply plugin: "maven-publish"

    // Standard Thule gradle plugin config
    apply from: "${rootDir}/../gradle/build-config.gradle"

    // Non-standard Thule gradle plugin config
    sonarqube {
        properties {
            // SonarQube will fail due to duplicate code across micro-services, however, this is legal. So we will exclude cpd for those classes
            property "sonar.cpd.exclusions", "**/domain/**"
        }
    }

    // Dependencies
    dependencyManagement {
        imports {
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:Greenwich.RELEASE")
        }
    }

    dependencies {
        compile("uk.co.serin.thule:thule-spring-boot-starter-oauth2:1.0.0-SNAPSHOT")
        compile("uk.co.serin.thule:thule-spring-boot-starter-utils:1.0.0-SNAPSHOT")
        compile("com.googlecode.log4jdbc:log4jdbc:1.2")
        compile("javax.xml.bind:jaxb-api") // Required by Hibernate (jaxb) when using java 9+
        compile("mysql:mysql-connector-java")
        compile("org.flywaydb:flyway-core")
        compile("org.jolokia:jolokia-core") // Required for JMX support when using the Admin server
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.boot:spring-boot-starter-data-jpa")
        compile("org.springframework.boot:spring-boot-starter-data-rest")
        compile("org.springframework.boot:spring-boot-starter-validation")
        compile("org.springframework.cloud:spring-cloud-starter-config")
        compile("org.springframework.cloud:spring-cloud-starter-netflix-eureka-client")
        compile("org.springframework.cloud:spring-cloud-starter-openfeign")
        compile("org.springframework.cloud:spring-cloud-starter-sleuth")
        compile("org.springframework.cloud:spring-cloud-starter-zipkin")
        compile("org.springframework.data:spring-data-rest-hal-browser")
        // Required to retry connecting to the configuration service when it is not available at startup
        compile("org.springframework.retry:spring-retry")
        // Required due to a bug in spring cloud which has a dependency on a milestone release for spring-security-oauth2-autoconfigure:2.1.0.M4
        compile("org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:2.1.2.RELEASE")

        testCompile("uk.co.serin.thule:thule-spring-boot-starter-test:1.0.0-SNAPSHOT")

        itestCompile project(":thule-people-config")
        itestCompile("uk.co.serin.thule:thule-spring-boot-starter-oauth2-test:1.0.0-SNAPSHOT")
        itestCompile("com.h2database:h2")
        itestCompile("com.oracle:ojdbc6:11.2.0.1.0")
        itestCompile("org.hsqldb:hsqldb")

        ctestCompile project(":thule-people-config")
        ctestCompile("uk.co.serin.thule:thule-spring-boot-starter-oauth2-test:1.0.0-SNAPSHOT")

        dtestCompile("uk.co.serin.thule:thule-spring-boot-starter-oauth2:1.0.0-SNAPSHOT")
        dtestCompile("uk.co.serin.thule:thule-spring-boot-starter-oauth2-test:1.0.0-SNAPSHOT")
        dtestCompile("uk.co.serin.thule:thule-spring-boot-starter-test:1.0.0-SNAPSHOT")
        dtestCompile("uk.co.serin.thule:thule-spring-boot-starter-utils:1.0.0-SNAPSHOT")
        dtestCompile("com.googlecode.log4jdbc:log4jdbc:1.2")
        dtestCompile("mysql:mysql-connector-java")
        dtestCompile("org.springframework.boot:spring-boot-starter-jdbc")
        dtestCompile("org.springframework.boot:spring-boot-starter-web")
        dtestCompile("org.springframework.cloud:spring-cloud-starter-oauth2")
    }
}