buildscript {
    repositories {
        maven { url "http://pooh:8081/repository/maven-public" }
        mavenLocal()
    }

    // Changing modules are cached by default for 24 hours, change the default to never and mark snapshots as changing (must be within the buildscript)
    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.17.0" apply(false)
    id "com.palantir.docker" version "0.19.2" apply(false)
    id "org.sonarqube" version "2.6.2" apply(false)
    id "org.springframework.boot" version "2.0.0.RELEASE" apply(false)
}
apply plugin: "io.spring.dependency-management"

subprojects {
    apply plugin: "com.github.ben-manes.versions"
    apply plugin: "com.palantir.docker"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "jacoco"
    apply plugin: "java"
    apply plugin: "maven-publish"
    apply plugin: "org.sonarqube"
    apply plugin: "org.springframework.boot"

    // Standard Serin gradle plugin config
    apply from: "${rootDir}/../gradle/build-config.gradle"

    // Non-standard Serin gradle plugin config
    tasks.docker.enabled = false // Don't need a docker image created
    tasks.dockerTag.enabled = false // Don't need a docker image created
    bootJar.enabled = false // Don't need an "über-jar" but still want the rest of the spring boot plugin
    jar.enabled = true // Don't need an "über-jar" but still want the rest of the spring boot plugin
}

// Dependencies
dependencyManagement {
    imports {
        mavenBom("org.springframework.cloud:spring-cloud-dependencies:Finchley.M9")
    }
}

project("thule-spring-boot-starter-test") {
    description = "Thule Spring Boot Starter Test"
    dependencies {
        compile project(":thule-test")
        compile("nl.jqno.equalsverifier:equalsverifier:2.4.5")
        compile("org.awaitility:awaitility:3.1.0")
        compile("org.springframework.boot:spring-boot-starter-test")
        compile("org.springframework.cloud:spring-cloud-contract-wiremock:2.0.0.M8")
        compile("pl.pojo:pojo-tester:0.7.6")
    }
}

project("thule-spring-boot-starter-utils") {
    description = "Thule Spring Boot Starter Utils"
    dependencies {
        compile project(":thule-utils")

        testCompile project(":thule-spring-boot-starter-test")
    }
}

project("thule-test") {
    description = "Thule Test"
    dependencies {
        compile("org.assertj:assertj-core")
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.boot:spring-boot-starter-test")
        compile("org.springframework.boot:spring-boot-starter-web")
        compile("org.springframework.retry:spring-retry")

        testCompile("pl.pojo:pojo-tester:0.7.6")
    }
}

project("thule-utils") {
    description = "Thule Utils"
    dependencies {
        compile("org.codehaus.janino:janino:3.0.8") // Required by logback if-condition statements in the xml configuration
        compile("org.springframework.boot:spring-boot-starter-aop")
        compile("org.springframework.boot:spring-boot-starter-logging")
        compile("org.springframework.boot:spring-boot-starter-web") 

        testCompile project(":thule-spring-boot-starter-test")
    }
}
